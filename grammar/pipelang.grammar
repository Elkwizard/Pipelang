comment = hidden /\s+|\/\/.*|\/\*([\w\W]*?)\*\//
string = /"((\\.)*([\w\W]*?))*?"/
char = /'\\?.'/
symbol = /[{}()\[\];,:.$]|\|>|(=(?!=))/
number = /\-?\b(\d+(\.\d+)?|\.\d+)([eE][\+\-]?\d+)?\b/g
identifier = /\w+|\|+|[^\w\s(){}\[\]|'",:;.$]+/

// base expressions
Reference = name:identifier
NumberValue = value:number
StringValue = value:string
CharValue = value:char
Value = NumberValue | StringValue | CharValue
Field = key:identifier ":" value:Precedence?
List = "{" (elements:Precedence[","?] | elements:Field[","?]) "}"
Parameter = type:Expression? name:Name (":" value:Pipe)? ("where" guard:Pipe)?
Operator = "[" (parameters:Parameter[","] "=" body:Body | body:Precedence) "]"
Nested = "$"? "(" replace:Precedence ")"
BaseExpression = Nested | Operator | List | Reference | Value
Index = "(" start:Precedence? ":" end:Precedence? ")"
Arguments = "(" arguments:Precedence[","] ")"
Property = "." key:identifier
Overload = "&" overload:Expression
Suffix = Arguments | Index | Property | Overload | Cast
Expression = base:Expression step:Suffix | BaseExpression

// precedence expressions
Precedence = operators Expression {
	prefix Prefix ("-" | "/" | "!" | "++" | "--")
	right Composition ("of")
	right Exponential ("^" | "**")
	left Product ("*" | "/" | "%")
	left Sum ("+" | "-" | "#")
	left Type ("in")
	left Compare ("==" | "!=" | "===" | "!==" | "<" | "<=" | ">" | ">=")
	left Logic ("&&" | "||" | "??")
	custom Conditional (condition:last "?" ifTrue:Precedence ":" ifFalse:Conditional)
	custom Pipe (source:Pipe "|>" step:(Alias | Reset | Call))
	custom Assignment (isClass:"class"? target:Name op:("&=" | "=") value:Assignment)
}

// destructuring
ListDestructure = "{" names:Name[","?] "}"
DestructureField = key:identifier ":" name:Name?
ObjectDestructure = "{" fields:DestructureField[","?] "}"
WrappedName = "[" name:Name "]"
Name = WrappedName | ListDestructure | ObjectDestructure | identifier

// steps
Alias = overload:"&"? "is" isClass:"class"? name:Name
Reset = "to" value:Expression
Call = operator:Expression arguments:Expression*

// statements
Link = "link" name:identifier "=" source:identifier
Body = statements:(Link | Precedence){";"} ";"?

root = Body